* Model using LTspice Version 4 or later
* GaNPower Int'l Inc. EHEMT
* Level 10  Version 1.0                                          *
*                                                               *
* Model constructed based on published sesearch papers. 
* GaNPower Int'l does not assume liability for its use.
* For tech questions, please contact www.iganpower.com
*                                                               *
* Key reference:
* H. Li, X. Zhao, W. Su, K. Sun, X. You and T. Q. Zheng, 
* "Nonsegmented PSpice Circuit Model of GaN HEMT With Simulation 
* Convergence Consideration," in IEEE Transactions on Industrial 
* Electronics," vol. 64, no. 11, pp. 8992-9000, Nov. 2017.
* 
*  Model calibrated using data EHEMT 650V 30A 
*  Model name GSI65030
*****************************************************************
*
.subckt GPI65030TOL gatein drainin sourcein
*
*****************************************************************
*	USER DEFINE max_curr
.param hemt_max_cur=30.



*****************************************************************
* DO NOT ALTER THE FOLOWING INTERNAL PARAMETERS
.param hemt_ref_cur=15.
.param hemt_w=hemt_max_cur*182000e-6/hemt_ref_cur hemt_l=20.2e-6
*****************************************************************
.param w_ref=182000e-6	alch_ref=20.2e-6 finger_sd_ref=2e-6

.param area_mesa_ref=w_ref*(alch_ref+finger_sd_ref)
.param hemt_l10_sd_finger=2e-6
.param area_mesa=hemt_w*(hemt_l+hemt_l10_sd_finger)
.param ai_fac=hemt_w/w_ref cap_fac=area_mesa/area_mesa_ref vds_fac=alch_ref/hemt_l
+		res_fac=1.5*1/(ai_fac*vds_fac)


.param K1_cur={1.6*ai_fac}   c1_slp=2.0   xmx0_0=0.5  xmx0_1=0.2
.param delta_Vt=0
*
*
rd drainin drain {0.05*res_fac} tc=0.0135
rs sourcein source {0.02*res_fac} tc=0.0135
rg gatein gate {0.38*res_fac}
*
*
bswitch drain source I = (if (v(drain,source)>0,
+	(K1_cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(26*(v(gate,source)-(7.9+delta_Vt)+6.2-0.00*(Temp-25))/c1_slp))*
+	vds_fac*v(drain,source)/(1 + max(xmx0_0+xmx0_1*(v(gate,source)+4.1),0.2)*vds_fac*v(drain,source))),
+	(-K1_cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,drain)-(7.9+delta_Vt)+6.2-0.00*(Temp-25))/c1_slp))*
+	vds_fac*v(source,drain)/(1 + max(xmx0_0+xmx0_1*(v(gate,drain)+6.1),0.2)*1.0*vds_fac*v(source,drain)))) )
*
*
C_GS	 gate source  	{119.0e-12*cap_fac}
*
C_GD   gate drain    	{4.0e-12*cap_fac}
*
C_SD	 source drain  	{25.0e-12*cap_fac}

.ends
*$
